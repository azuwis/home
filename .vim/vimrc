" NeoBundle Start{{{1
let s:bundledir = expand('~/.vim/bundle/')
if has('vim_starting')
  if !isdirectory(s:bundledir . '/neobundle/')
    let s:initcmd = 'git clone https://github.com/Shougo/neobundle.vim.git ' . s:bundledir . '/neobundle/'
    echomsg '[neobundle/install] (0): |neobundle| ' . s:initcmd
    if !isdirectory(s:bundledir)
      call mkdir(s:bundledir)
    endif
    call system(s:initcmd)
  endif
  set nocompatible
  set runtimepath+=~/.vim/bundle/neobundle/
endif
call neobundle#begin(s:bundledir)
NeoBundleFetch 'Shougo/neobundle.vim', {'directory': 'neobundle'}

" Sensible{{{1
" https://github.com/tpope/vim-sensible
set autoindent
set backspace=indent,eol,start
set complete-=i
set smarttab

set nrformats-=octal

set ttimeout
set ttimeoutlen=100

set incsearch

set laststatus=2
set ruler
set showcmd
set wildmenu

set scrolloff=3
set sidescrolloff=5
set display+=lastline

set listchars=tab:>\ ,trail:-,extends:>,precedes:<,nbsp:+

setglobal tags-=./tags tags^=./tags;

set autoread
set fileformats+=mac

set history=1000
set tabpagemax=50
set viminfo^=!
set sessionoptions-=options

runtime! macros/matchit.vim

inoremap <C-U> <C-G>u<C-U>

" Options {{{1

"set showbreak=↪
"set listchars+=tab:▸\ 
"set listchars+=eol:¬
"highlight NonText guifg=#4a4a59
"highlight SpecialKey guifg=#4a4a59

set modeline
"set relativenumber
"set cursorline
set colorcolumn=80
" disable startup message
set shortmess+=I
set fileencodings=ucs-bom,utf-8,cp936,default,latin1
set ignorecase
set smartcase
set hlsearch
set wildignore=
set splitright splitbelow
set lazyredraw

" Always backup {{{2
let s:savedir = expand('~/.vim/save')
if !isdirectory(s:savedir)
  call mkdir(s:savedir, 'p', 0700)
  call mkdir(s:savedir . '/undo/')
  call mkdir(s:savedir . '/backup/')
  call mkdir(s:savedir . '/swap/')
endif
set swapfile
let &directory = s:savedir . '/swap/'
set undofile
let &undodir = s:savedir . '/undo/'
set backup
let &backupdir = s:savedir . '/backup/'

" Mappings {{{1
map Q gq
nnoremap <F5> :update<CR>
inoremap <F5> <C-o>:update<CR>
"nnoremap / /\v
"vnoremap / /\v
nnoremap <silent> <leader>s :set spell!<CR>
"nnoremap ; :
inoremap kj <ESC>
" man
"runtime ftplugin/man.vim
"nmap K <Leader>K

" Quick toggles {{{2
nnoremap <silent> <leader>l :set list!<CR>
nnoremap <silent> <leader>n :silent :nohlsearch<CR>

" Window switching {{{2
nnoremap <C-k> <C-W>k
nnoremap <C-j> <C-W>j
nnoremap <C-h> <C-W>h
nnoremap <C-l> <C-W>l

" File opening {{{2
" Shortcuts for opening file in same directory as current file
cnoremap <expr> %% getcmdtype() == ':' ? escape(expand('%:h'), ' \').'/' : '%%'
map <leader>ew :e %%
map <leader>es :sp %%
map <leader>ev :vsp %%
map <leader>et :tabe %%
" Prompt to open file with same name, different extension
map <leader>er :e <C-R>=expand("%:r")."."<CR>

" Fix the & command in normal+visual modes {{{2
nnoremap & :&&<Enter>
xnoremap & :&&<Enter>

" Recall command history {{{2
cnoremap <C-p> <Up>
cnoremap <C-n> <Down>

" Indentation {{{1
set ts=8 sts=4 sw=4 et
" http://vimcasts.org/episodes/tabs-and-spaces/
" Set tabstop, softtabstop and shiftwidth to the same value
command! -nargs=* Stab call Stab()
function! Stab()
  let l:tabstop = 1 * input('set tabstop = softtabstop = shiftwidth = ')
  if l:tabstop > 0
    let &l:sts = l:tabstop
    let &l:ts = l:tabstop
    let &l:sw = l:tabstop
  endif
  call SummarizeTabs()
endfunction
function! SummarizeTabs()
  try
    echohl ModeMsg
    echon 'tabstop='.&l:ts
    echon ' shiftwidth='.&l:sw
    echon ' softtabstop='.&l:sts
    if &l:et
      echon ' expandtab'
    else
      echon ' noexpandtab'
    endif
  finally
    echohl None
  endtry
endfunction

" Autocmd {{{1
if has("autocmd")
  augroup vimrcEX
    autocmd!
    " space and tab
    autocmd FileType make setlocal ts=8 sts=8 sw=8 noet
    autocmd FileType vim,yaml,html,css,ruby,eruby,sh setlocal ts=2 sts=2 sw=2 et
    autocmd FileType javascript setlocal ts=4 sts=4 sw=4 noet

    " filetype specific mapping
    autocmd FileType vim nnoremap <buffer> K :h <C-r><C-w><CR>

    " no list and relativenumber for manpages
    autocmd FileType man setlocal nolist norelativenumber colorcolumn=

    " treat .rss files as XML
    autocmd BufNewFile,BufRead *.rss setfiletype xml

    " treat .mrconfig files as cfg, ~/.mrlib as sh
    autocmd BufNewFile,BufRead */.mrconfig setfiletype cfg
    autocmd BufNewFile,BufRead ~/.mrlib setfiletype sh

    " .adoc as asciidoc
    autocmd BufNewFile,BufRead *.adoc,*.asc setfiletype asciidoc

    " set spell for git commit msg
    autocmd FileType gitcommit setlocal spell

    " auto save file on losing focus
    autocmd FocusLost * silent! :update

    " auto load vimrc
    autocmd BufWritePost ~/.vim/vimrc source $MYVIMRC

    " restore cursor position
    autocmd BufReadPost *
      \ if line("'\"") > 1 && line("'\"") <= line("$") |
      \   exe "normal! g`\"" |
      \ endif

    " load ~/.Xresources if modified
    autocmd BufWritePost ~/.Xresources :silent !xrdb ~/.Xresources
  augroup END
endif

" Commands to quickly set >1 option in one go {{{2
command! -nargs=* Wrap set wrap linebreak nolist


if !exists(":DiffOrig")
  command DiffOrig vert new | set bt=nofile | r ++edit # | 0d_ | diffthis
    \ | wincmd p | diffthis
endif

" Recipe {{{1
" Clean SRT {{{2
command! CleanSRT normal :%s/{[^}]*}//g<CR>

" Remove trailing whitespace {{{2
" http://www.bestofvim.com/tip/trailing-whitespace/
"match ErrorMsg '\s\+$'
match ErrorMsg /\s\+\%#\@<!$/
command! RTW normal :%s/\s\+$//e<CR>

" Auto create parent dir {{{2
" https://github.com/dockyard/vim-easydir
augroup easydir
  au!
  au BufWritePre,FileWritePre * call <SID>create_and_save_directory()
augroup END
function! <SID>create_and_save_directory()
  let s:directory = expand('<afile>:p:h')
  if !isdirectory(s:directory)
    call mkdir(s:directory, 'p')
  endif
endfunction

" General Bundles {{{1
" vimproc
NeoBundle 'Shougo/vimproc.vim', {'directory': 'vimproc',
      \ 'build' : {
      \     'windows' : 'tools\\update-dll-mingw',
      \     'cygwin' : 'make -f make_cygwin.mak',
      \     'mac' : 'make -f make_mac.mak',
      \     'unix' : 'make -f make_unix.mak',
      \    },
      \ }

" airline
let g:airline_left_sep = ''
let g:airline_right_sep = ''
NeoBundle 'bling/vim-airline', {'directory': 'airline'}

" zenburn
NeoBundle 'jnurmine/Zenburn', {'directory': 'zenburn'}

" gundo
NeoBundle 'sjl/gundo.vim', {'directory': 'gundo'}
map <Leader>u :GundoToggle<CR>

" fugitive
NeoBundle 'tpope/vim-fugitive', {'directory': 'fugitive'}

" signify
NeoBundle 'mhinz/vim-signify', {'directory': 'signify'}

" repeat
NeoBundle 'tpope/vim-repeat', {'directory': 'repeat'}

" surround
NeoBundle 'tpope/vim-surround', {'directory': 'surround'}

" easymotion
NeoBundle 'Lokaltog/vim-easymotion', {'directory': 'easymotion'}

" syntastic
NeoBundleLazy 'scrooloose/syntastic', {'filetypes': [
      \ 'sh',
      \ 'python',
      \ 'ruby',
      \ 'vim',
      \ ]}

" neocomplete
if has("lua")
  let g:neocomplete#enable_at_startup = 1
  NeoBundle 'Shougo/neocomplete.vim', {'directory': 'neocomplete'}
else
  let g:neocomplcache_enable_at_startup = 1
  NeoBundle 'Shougo/neocomplcache.vim', {'directory': 'neocomplcache'}
endif

" Filetypes {{{1
" Ansible
NeoBundle 'chase/vim-ansible-yaml', {'directory': 'ansible'}

" NeoBundle End {{{1
call neobundle#end()
NeoBundleCheck

" Ending configs {{{1
" colorscheme
try
  colorscheme zenburn
catch /^Vim\%((\a\+)\)\=:E185/
  colorscheme slate
endtry
" syntax highlight
filetype plugin indent on
syntax enable

" vim: fdm=marker
